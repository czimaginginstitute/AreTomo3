#include "CReconInc.h"
#include <memory.h>
#include <stdio.h>

using namespace Recon;

extern __shared__ char s_acArray[];

static __global__ void mGCalcRFactor
(	int iProjX,
	float* gfProj, // full projection, not y-slice
	float* gfSum,  // size of iProjY
	int* giCount   // size of iProjY
)
{	float* sfSum = (float*)&s_acArray[0];
	int* siCount = (int*)&sfSum[blockDim.x];
	sfSum[threadIdx.x] = 0.0f;
	siCount[threadIdx.x] = 0;
	__syncthreads();
	//--------------
	int iSegments = iProjX / blockDim.x + 1;
	for(int i=0; i<iSegments; i++)
	{	int x = i * blockDim.x + threadIdx.x;
		if(x >= iProjX) continue;
		float fVal = gfProj[blockIdx.y * iProjX + x];
		if(fVal > -1e20)
		{	sfSum[threadIdx.x] += fabs(fVal);
			siCount[threadIdx.x] += 1;
		}
	}
	__syncthreads();
	//--------------
	int i = blockDim.x / 2;
	while(i > 0)
	{	if(threadIdx.x < i)
		{	sfSum[threadIdx.x] += sfSum[i+threadIdx.x];
			siCount[threadIdx.x] += siCount[i+threadIdx.x];
		}
		__syncthreads();
		i /= 2;
	}
	//-------------
	if(threadIdx.x == 0)
	{	gfSum[blockIdx.y] = sfSum[0];
		giCount[blockIdx.y] = siCount[0];
	}
}

static __global__ void mGCalcRFactorSum
(	int iSize,
	float* gfSum,
	int* giCount
)
{	float* sfSum = (float*)&s_acArray[0];
	int* siCount = (int*)&sfSum[blockDim.x];
	sfSum[threadIdx.x] = 0.0f;
	siCount[threadIdx.x] = 0;
	__syncthreads();
	//--------------
	int iSegments = iSize / blockDim.x + 1;
	for(int i=0; i<iSegments; i++)
	{	int j = i * blockDim.x + threadIdx.x;
		if(j >= iSize) continue;
		sfSum[threadIdx.x] += gfSum[j];
		siCount[threadIdx.x] += giCount[j];
	}
	__syncthreads();
	//--------------
	int i = blockDim.x / 2;
	while(i > 0)
	{	if(threadIdx.x < i)
		{	sfSum[threadIdx.x] += sfSum[i+threadIdx.x];
			siCount[threadIdx.x] += siCount[i+threadIdx.x];
		}
		__syncthreads();
		i /= 2;
	}
	//-------------
	if(threadIdx.x == 0)
	{	gfSum[0] = sfSum[0];
		giCount[0] = siCount[0];
	}
}	

GCalcRFactor::GCalcRFactor(void)
{
	m_gfSum = 0L;
	m_giCount = 0L;
}

GCalcRFactor::~GCalcRFactor(void)
{
	this->Clean();
}

void GCalcRFactor::Clean(void)
{
	if(m_gfSum != 0L) cudaFree(m_gfSum);
	if(m_giCount != 0L) cudaFree(m_giCount);
	m_gfSum = 0L;
	m_giCount = 0L;
}

void GCalcRFactor::Setup(int iProjX, int iNumProjs)
{
	this->Clean();
	m_iProjX = iProjX;
	m_iNumProjs = iNumProjs;
	cudaMalloc(&m_gfSum, sizeof(float) * m_iNumProjs);
	cudaMalloc(&m_giCount, sizeof(int) * m_iNumProjs);
}

float GCalcRFactor::DoIt(float* gfProj)
{
	dim3 aBlockDim(512, 1);
	dim3 aGridDim(1, m_iNumProjs);
	int iShmBytes = (sizeof(float) + sizeof(int)) * aBlockDim.x;
	mGCalcRFactor<<<aGridDim, aBlockDim, iShmBytes>>>
	( m_iProjX, gfProj, m_gfSum, m_giCount
	);
	//------------------------------------
	aGridDim.y = 1;
	mGCalcRFactorSum<<<aGridDim, aBlockDim, iShmBytes>>>
	( m_iNumProjs, m_gfSum, m_giCount
	);
	//-------------------------------
	cudaMemcpy(&m_fRFactorSum, m_gfSum, sizeof(float), cudaMemcpyDefault);
	cudaMemcpy(&m_iCount, m_giCount, sizeof(int), cudaMemcpyDefault);
	if(m_iCount == 0) return 0.0f;
	else return m_fRFactorSum / m_iCount;
}
	
